/*
 * PROJET : REMOTE LORA - VERSION DEBUG (Série Activé)
 * CIBLE  : Seeed Studio XIAO ESP32C6
 */

#include <Arduino.h>
#include <SPI.h>
#include <LoRa.h>
#include <WiFi.h>
#include <WebServer.h>
#include "driver/gpio.h"

// --- CONFIGURATION LORA (Ra-02) ---
#define DEVICE_ID           0x77 
#define LORA_FREQUENCY      433E6

// REGLAGES
#define LORA_SF             11     
#define LORA_BW             125E3  
#define LORA_CR             5      
#define LORA_TX_POWER       17     
#define LORA_SYNC_WORD      0xF3

// PROTOCOLE
#define CMD_RELAY_ON        0xA1
#define CMD_RELAY_OFF       0xB2
#define ACK_RELAY_IS_ON     0xD4
#define ACK_RELAY_IS_OFF    0xE5

// PINOUT
#define PIN_LORA_RST    D0
#define PIN_LORA_NSS    D1
#define PIN_LORA_DIO0   D2
#define PIN_BUTTON      D3
#define PIN_LORA_MOSI   D4
#define PIN_LORA_MISO   D5
#define PIN_LORA_SCK    D6

// LEDS
#define PIN_LED_BLUE    D8
#define PIN_LED_RED     D9
#define PIN_LED_GREEN   D10

// TIMINGS
#define FEEDBACK_DURATION   10000 
#define ERROR_DURATION      3000  
#define WIFI_TIMEOUT_MS     300000 

WebServer server(80);
volatile unsigned long lastActivityTime = 0;
String webStatus = "EN ATTENTE...";

// MEMOIRE PERSISTANTE
RTC_DATA_ATTR bool nextActionIsOn = true; 
RTC_DATA_ATTR int bootCount = 0; 

const char index_html[] PROGMEM = R"rawliteral(
<!DOCTYPE HTML><html><head><title>Remote Sport</title>
<meta name="viewport" content="width=device-width,initial-scale=1">
<style>body{font-family:sans-serif;background:#121212;color:#eee;text-align:center;padding:20px}
.btn{width:100%;padding:20px;margin:10px 0;font-size:20px;border:none;border-radius:5px;color:#fff;cursor:pointer}
.on{background:#27ae60}.off{background:#c0392b}.ref{background:#2980b9}</style>
</head><body><h1>CONTROLE TERRAIN</h1><p>Etat: <b>%STATUS%</b></p>
<a href="/on"><button class="btn on">ALLUMER</button></a><a href="/off"><button class="btn off">ETEINDRE</button></a>
<a href="/status"><button class="btn ref">ACTUALISER</button></a></body></html>)rawliteral";

void ledsOff() {
    digitalWrite(PIN_LED_BLUE, LOW);
    digitalWrite(PIN_LED_RED, LOW);
    digitalWrite(PIN_LED_GREEN, LOW);
}

void signalTx() {
    digitalWrite(PIN_LED_BLUE, HIGH); delay(30); digitalWrite(PIN_LED_BLUE, LOW);
}

void signalError() {
    ledsOff();
    Serial.println("ERREUR / NO ACK");
    for(int i=0; i<3; i++) {
        digitalWrite(PIN_LED_RED, HIGH); delay(80);
        digitalWrite(PIN_LED_RED, LOW); delay(80);
    }
}

void initLoRa() {
    SPI.begin(PIN_LORA_SCK, PIN_LORA_MISO, PIN_LORA_MOSI, PIN_LORA_NSS);
    LoRa.setPins(PIN_LORA_NSS, PIN_LORA_RST, PIN_LORA_DIO0);
    if (!LoRa.begin(LORA_FREQUENCY)) {
        Serial.println("ERREUR INIT LORA !");
        return;
    }
    LoRa.setSpreadingFactor(LORA_SF);
    LoRa.setSignalBandwidth(LORA_BW);
    LoRa.setCodingRate4(LORA_CR);
    LoRa.setSyncWord(LORA_SYNC_WORD);
    LoRa.setTxPower(LORA_TX_POWER);
}

int sendCmd(uint8_t cmd) {
    for(int attempt = 1; attempt <= 3; attempt++) {
        signalTx();
        Serial.print("Envoi Tentative "); Serial.println(attempt);
        
        LoRa.beginPacket(); LoRa.write(DEVICE_ID); LoRa.write(cmd); LoRa.endPacket();
        
        unsigned long s = millis();
        while(millis() - s < 2000) {
            if(LoRa.parsePacket()) {
                if(LoRa.available() >= 2) {
                    uint8_t id = LoRa.read(); uint8_t r = LoRa.read();
                    if(id == DEVICE_ID) {
                        if(r == ACK_RELAY_IS_ON) return 1;
                        if(r == ACK_RELAY_IS_OFF) return 2;
                    }
                }
            }
        }
        delay(150); 
    }
    return 0; 
}

bool smartWait(unsigned long durationMs) {
    unsigned long start = millis();
    while(digitalRead(PIN_BUTTON) == LOW) { delay(10); } 
    while(millis() - start < durationMs) {
        if(digitalRead(PIN_BUTTON) == LOW) { delay(50); return true; }
        delay(10); 
    }
    return false; 
}

bool processAction() {
    Serial.println(">>> Traitement Action...");
    unsigned long start = millis();
    
    // --- GESTION WIFI ---
    // Tant que le bouton est appuyé, on compte
    while(digitalRead(PIN_BUTTON) == LOW) {
        unsigned long duration = millis() - start;
        
        // Clignotement bleu pour dire "Je compte..."
        if((duration / 200) % 2 == 0) digitalWrite(PIN_LED_BLUE, HIGH);
        else digitalWrite(PIN_LED_BLUE, LOW);

        if(duration > 5000) {
            // MODE WIFI ACTIVÉ
            digitalWrite(PIN_LED_BLUE, HIGH); // Bleu Fixe
            Serial.println("--- MODE WIFI AP ---");
            
            initLoRa();
            WiFi.softAP("TELECOMMANDE_SPORT", "admin1234");
            Serial.print("IP: "); Serial.println(WiFi.softAPIP());
            
            server.on("/", [](){ String s=index_html; s.replace("%STATUS%", webStatus); server.send(200, "text/html", s); lastActivityTime=millis(); });
            server.on("/on", [](){ int res = sendCmd(CMD_RELAY_ON); if(res==1) nextActionIsOn=false; webStatus=(res==1)?"ALLUME":"ERREUR"; server.sendHeader("Location","/"); server.send(303); lastActivityTime=millis(); });
            server.on("/off", [](){ int res = sendCmd(CMD_RELAY_OFF); if(res==2) nextActionIsOn=true; webStatus=(res==2)?"ETEINT":"ERREUR"; server.sendHeader("Location","/"); server.send(303); lastActivityTime=millis(); });
            server.begin(); 
            
            lastActivityTime = millis();
            while(true) { 
                server.handleClient(); 
                if(millis()-lastActivityTime > WIFI_TIMEOUT_MS) { 
                    Serial.println("WiFi Timeout -> Fin");
                    WiFi.mode(WIFI_OFF); 
                    return false; 
                }
                delay(2); 
            }
        }
        delay(10);
    }
    ledsOff(); // On éteint le clignotement bleu si on relâche avant 5s

    // --- GESTION LORA ---
    long pressDuration = millis() - start;
    bool interrupted = false; 

    if(pressDuration > 50) {
        initLoRa();
        int result = 0;

        if (nextActionIsOn) {
            Serial.println("Demande -> ON");
            result = sendCmd(CMD_RELAY_ON);
        } else {
            Serial.println("Demande -> OFF");
            result = sendCmd(CMD_RELAY_OFF);
        }

        if (result == 1) {
            Serial.println("ACK: ON");
            digitalWrite(PIN_LED_GREEN, HIGH); 
            nextActionIsOn = false; 
            interrupted = smartWait(FEEDBACK_DURATION); 
        } 
        else if (result == 2) {
            Serial.println("ACK: OFF");
            digitalWrite(PIN_LED_RED, HIGH); 
            nextActionIsOn = true; 
            interrupted = smartWait(ERROR_DURATION); 
        } 
        else {
            signalError(); 
            interrupted = false; 
        }
    }
    
    ledsOff();
    return interrupted; 
}

void setup() {
    bootCount++;
    pinMode(PIN_LED_BLUE, OUTPUT);
    pinMode(PIN_LED_RED, OUTPUT);
    pinMode(PIN_LED_GREEN, OUTPUT);
    ledsOff();
    pinMode(PIN_BUTTON, INPUT_PULLUP);
    
    // --- FORCE SERIAL POUR DEBUG ---
    Serial.begin(115200);
    delay(1000); // Attente USB
    Serial.println("\n--- BOOT ---");
    
    // Si on démarre avec le bouton appuyé (Réveil ou branchement + bouton)
    if (digitalRead(PIN_BUTTON) == LOW) {
        bool runAgain = processAction();
        while(runAgain) { runAgain = processAction(); }
    } else {
        Serial.println("Bouton relâché. Mode Attente Debug (10s)...");
        // Petite fenêtre pour flasher ou voir les logs avant de dormir
        unsigned long s = millis();
        while(millis() - s < 10000) {
            if(digitalRead(PIN_BUTTON) == LOW) {
                 bool runAgain = processAction();
                 while(runAgain) { runAgain = processAction(); }
                 break;
            }
            delay(10);
        }
    }
}

void loop() {
    Serial.println("Attente relâchement bouton...");
    while(digitalRead(PIN_BUTTON) == LOW) delay(10);
    
    Serial.println(">>> MISE EN VEILLE (USB VA COUPER)");
    Serial.flush(); // On attend que le texte soit parti
    Serial.end();   // On coupe proprement l'USB

    ledsOff();
    LoRa.end();
    WiFi.mode(WIFI_OFF);
    
    gpio_set_direction((gpio_num_t)PIN_BUTTON, GPIO_MODE_INPUT);
    gpio_pullup_en((gpio_num_t)PIN_BUTTON);
    gpio_wakeup_enable((gpio_num_t)PIN_BUTTON, GPIO_INTR_LOW_LEVEL);
    esp_sleep_enable_gpio_wakeup();
    esp_light_sleep_start();

    // --- REVEIL ---
    // On relance l'USB
    Serial.begin(115200);
    unsigned long ws = millis();
    // On attend jusqu'à 2 sec que l'USB remonte, sinon on continue
    while(!Serial && (millis()-ws<2000)); 
    
    Serial.println("\n>>> REVEIL !");
    
    bool runAgain = processAction();
    while(runAgain) {
        runAgain = processAction();
    }
}
