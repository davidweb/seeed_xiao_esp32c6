/*
 * PROJET : ACTUATOR LORA - VERSION FINALE
 * CIBLE  : ESP32 Dev Module
 */

#include <Arduino.h>
#include <SPI.h>
#include <LoRa.h>

#define DEVICE_ID           0x77 
#define LORA_FREQUENCY      433E6
// --- NOUVEAUX REGLAGES PLUS RAPIDES ---
#define LORA_SF             10      // SF10 = Plus rapide
#define LORA_BW             250E3   // BW 250 = Plus tolérant
#define LORA_CR             8
#define LORA_TX_POWER       20
#define LORA_SYNC_WORD      0xF3

#define CMD_RELAY_ON        0xA1
#define CMD_RELAY_OFF       0xB2
#define CMD_GET_STATUS      0xC3
#define ACK_RELAY_IS_ON     0xD4
#define ACK_RELAY_IS_OFF    0xE5
#define AUTO_OFF_TIMEOUT    14400000 

// PINOUT ESP32 Standard
#define PIN_LORA_SS     5
#define PIN_LORA_RST    14
#define PIN_LORA_DIO0   2
#define PIN_RELAY       4
#define PIN_LORA_MOSI   23
#define PIN_LORA_MISO   19
#define PIN_LORA_SCK    18

QueueHandle_t cmdQueue;
unsigned long relayOnTime = 0;
bool isRelayOn = false;

// --- TÂCHE RELAIS ---
void taskRelay(void *pvParameters) {
    pinMode(PIN_RELAY, OUTPUT); 
    digitalWrite(PIN_RELAY, LOW); 

    uint8_t cmd;
    for (;;) {
        if (xQueueReceive(cmdQueue, &cmd, pdMS_TO_TICKS(1000)) == pdTRUE) {
            if (cmd == CMD_RELAY_ON) { 
                Serial.println("ACTION: RELAY ON");
                digitalWrite(PIN_RELAY, HIGH); 
                isRelayOn = true; 
                relayOnTime = millis(); 
            } 
            else if (cmd == CMD_RELAY_OFF) { 
                Serial.println("ACTION: RELAY OFF");
                digitalWrite(PIN_RELAY, LOW); 
                isRelayOn = false; 
            }
        }
        if (isRelayOn && (millis() - relayOnTime > AUTO_OFF_TIMEOUT)) { 
            digitalWrite(PIN_RELAY, LOW); isRelayOn = false; 
        }
    }
}

// --- TÂCHE RADIO ---
void taskLoRa(void *pvParameters) {
    SPI.begin(PIN_LORA_SCK, PIN_LORA_MISO, PIN_LORA_MOSI, PIN_LORA_SS);
    LoRa.setPins(PIN_LORA_SS, PIN_LORA_RST, PIN_LORA_DIO0);
    
    if (!LoRa.begin(LORA_FREQUENCY)) {
        Serial.println("LoRa Init Failed!"); vTaskDelete(NULL);
    }
    
    // Application des nouveaux réglages
    LoRa.setSpreadingFactor(LORA_SF);
    LoRa.setSignalBandwidth(LORA_BW);
    LoRa.setCodingRate4(LORA_CR);
    LoRa.setSyncWord(LORA_SYNC_WORD);
    LoRa.setTxPower(LORA_TX_POWER); // Puissance max pour l'ACK

    Serial.println("LoRa Ready (SF10/BW250)...");
    unsigned long lastPacketTime = 0;

    for (;;) {
        if (LoRa.parsePacket()) { 
            if(LoRa.available() >= 2) {
                uint8_t id = LoRa.read();
                uint8_t cmd = LoRa.read();
                uint8_t ack = 0;

                if (id == DEVICE_ID) {
                    Serial.print("RX CMD: 0x"); Serial.println(cmd, HEX);
                    
                    bool isNewAction = (millis() - lastPacketTime > 1000);
                    
                    if (cmd == CMD_RELAY_ON) { 
                        if(isNewAction) { xQueueSend(cmdQueue, &cmd, 0); lastPacketTime = millis(); }
                        ack = ACK_RELAY_IS_ON; 
                    } 
                    else if (cmd == CMD_RELAY_OFF) { 
                        if(isNewAction) { xQueueSend(cmdQueue, &cmd, 0); lastPacketTime = millis(); }
                        ack = ACK_RELAY_IS_OFF; 
                    }
                    else if (cmd == CMD_GET_STATUS) { 
                        ack = isRelayOn ? ACK_RELAY_IS_ON : ACK_RELAY_IS_OFF; 
                    }

                    if(ack != 0) {
                        // Délai réduit car SF10 est plus rapide
                        vTaskDelay(pdMS_TO_TICKS(50)); 
                        
                        LoRa.beginPacket(); 
                        LoRa.write(DEVICE_ID); 
                        LoRa.write(ack); 
                        LoRa.endPacket(); 
                        LoRa.receive(); 
                        Serial.println("-> ACK SENT");
                    }
                }
            }
        }
        vTaskDelay(pdMS_TO_TICKS(10)); 
    }
}

void setup() {
    Serial.begin(115200);
    cmdQueue = xQueueCreate(10, sizeof(uint8_t));
    xTaskCreatePinnedToCore(taskLoRa, "LoRa", 4096, NULL, 5, NULL, 1);
    xTaskCreatePinnedToCore(taskRelay, "Relay", 2048, NULL, 1, NULL, 0);
}

void loop() { vTaskDelete(NULL); }
